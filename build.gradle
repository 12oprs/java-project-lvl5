plugins {
	id 'org.springframework.boot' version '2.6.7'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'application'
	id 'jacoco'
	id 'checkstyle'
	id 'org.liquibase.gradle' version '2.1.0'
	id 'com.adarshr.test-logger' version '3.0.0'
	id 'idea'

	// Сборщик фронтенда
	id 'org.siouan.frontend-jdk11' version '6.0.0'
}

group = 'hexlet.code'
version = '1.0-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
	maven {
		url "https://plugins.gradle.org/m2/"
	}
}

application {
	mainClass = 'hexlet.code.app.AppApplication'
}

idea {
	module {
		sourceDirs += file('generated/')
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
 	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-devtools'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springdoc:springdoc-openapi-ui:1.6.9'
	implementation 'com.rollbar:rollbar-spring-webmvc:1.8.1'

	compileOnly 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'

	implementation 'org.liquibase:liquibase-core:4.11.0'
	liquibaseRuntime(
			sourceSets.main.output,
			'org.liquibase:liquibase-core:4.11.0',
			'org.liquibase.ext:liquibase-hibernate5:4.11.0',
			'org.springframework.boot:spring-boot-starter-data-jpa'
	)
	compileOnly 'org.projectlombok:lombok:1.18.24'

	def queryDslVersion = '5.0.0'
	implementation "com.querydsl:querydsl-core:${queryDslVersion}"
	implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
	annotationProcessor(
			"com.querydsl:querydsl-apt:${queryDslVersion}:jpa",
			'javax.annotation:javax.annotation-api',
			'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final',
			'org.projectlombok:lombok'
	)

	testImplementation(
			'org.springframework.boot:spring-boot-starter-security',
			'org.springframework.security:spring-security-test:5.7.1',
			'org.springframework.boot:spring-boot-starter-test',

			'org.bouncycastle:bcprov-jdk15on:1.70',
			'com.github.database-rider:rider-junit5:1.32.3'
	)
}

tasks.named('test') {
	useJUnitPlatform()
}

task stage(dependsOn: ['installDist', 'clean'])
installDist.mustRunAfter clean

jacocoTestReport {
    reports {
        xml.required = true
    }
}

diffChangeLog {
	dependsOn compileJava
}

liquibase {
	activities {
		all {
			changeLogFile 'src/main/resources/db/changelog/changelog-master.yml'
			url 'jdbc:h2:./taskManager'
			username 'sa'
			password ''
			referenceUrl 'hibernate:spring:hexlet.code.app.model' +
					'?dialect=org.hibernate.dialect.H2Dialect' +
					'&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy'
		}
	}
}

test {
	useJUnitPlatform()
}

testlogger {
	showStandardStreams = true
}

// Сборка фронтенда.
// Выполняется только если ничего не изменилось.
// *****************************************************************************
frontend {
	nodeVersion = '16.13.1'
	installScript = 'install'
	assembleScript = 'run build'
	packageJsonDirectory = file("${projectDir}/frontend")
}

def configFrontendTasks = {
	inputs.files(fileTree("$projectDir/frontend").exclude('build', 'node_modules'))
	outputs.dir("$buildDir/resources/main/static")
}

assembleFrontend {
	configure configFrontendTasks

	doLast {
		copy {
			from "$projectDir/frontend/build"
			into "$buildDir/resources/main/static"
		}
	}
}

installFrontend {
	configure configFrontendTasks
}

processResources.dependsOn assembleFrontend
// *****************************************************************************